# electron perfect drag and drop : una implementación más elegante que electron-drag

El responsable del producto pidió que la barra de herramientas bajo la ventana de electrones bloqueara el botón derecho del ratón.

La función nativa de arrastrar y soltar en electrón bajo ventana ( `app-region:drag`) se come el evento de clic y no hay manera de hacer el bloqueo del clic derecho. Hay que implementar el drag and drop manualmente.

Si se utiliza el evento mousemove para implementar el requisito, cuando el ratón se mueve rápidamente, sale del alcance de la ventana y ésta ya no sigue al ratón.

He implementado este requisito basado en el evento del puntero, utilizando [setPointerCapture](https://developer.mozilla.org/zh-CN/docs/Web/API/Element/setPointerCapture) para capturar la posición del ratón fuera del BrowserWindow, y funciona perfectamente.

Esta solución es mejor que la de electron-drag: tiene dependencias binarias dependientes de la plataforma y puede ser un dolor para actualizar y empaquetar.

He descubierto que nadie ha mencionado aún esta solución en la web, así que la estoy publicando en el blog y compartiéndola.

Las diferencias de código entre las implementaciones se enumeran a continuación sólo como referencia. El código del núcleo es `web/src/lib/drag.coffee` y `main/ipc/drag.coffee`.

## web/src/lib/drag.coffee

```coffee
import $on from '~/lib/on.coffee'
import ipc from '~/lib/ipc.coffee'
import platform from '@/config/platform.mjs'

{drag:{setBounds,getBounds}} = ipc
pointermove = 'pointermove'
IGNORE = new Set('SELECT BUTTON A INPUT TEXTAREA'.split ' ')
{round} = Math
export default main = (elem)=>
  if platform!='win32'
    return

  elem.style.appRegion = 'no-drag'

  moving = false

  init_w = init_h = init_x = init_y = init_top = init_left = undefined

  _move = (e)=>
    {screenX,screenY} = e
    setBounds(
      round screenX-init_x+init_left
      round screenY-init_y+init_top
      init_w
      init_h
    )
    return

  down = (e)=>
    if moving
      return
    if e.button!=0 # 鼠标左键
      return
    {target} = e
    p = e.target
    loop
      {nodeName} = p
      if IGNORE.has nodeName
        return
      if nodeName == 'BODY'
        break
      p = p.parentNode
    moving = true
    {screenX:init_x,screenY:init_y} = e

    elem.setPointerCapture e.pointerId
    elem.addEventListener pointermove,_move

    {
      x:init_left
      y:init_top
      height:init_h
      width:init_w
    } = await getBounds()

    console.log  await getBounds()
    return

  up = (e)=>
    if moving
      await _move(e)
      elem.releasePointerCapture e.pointerId
      elem.removeEventListener pointermove,_move
      moving = false
    return

  $on elem,{
    lostpointercapture:up
    pointercancel:up
    pointerdown:down
    pointerup:up
  }

  return
```

## main/ipc/drag.coffee

```coffee
export getBounds = ->
  @sender.getOwnerBrowserWindow().getBounds()

export setBounds = (x,y,width,height)->
  win = @sender.getOwnerBrowserWindow()
  # 不用 setPosition 是因为 https://github.com/electron/electron/issues/9477 browserWindow.setPosition(x,y) changed window size (windows/linux) with non default scaleLevel (125% for example)
  win.setBounds {
    x:Math.round(x)
    y:Math.round(y)
    width
    height
  }
  return
```

## main/ipc.coffee

```coffee
`export * as drag from './ipc/drag.coffee'`
```

## web/src/page/recbar.vue

```pug
<template lang="pug">
-nav(:class="{ pause }")
nav(:class="{ pause }" ref="nav")
</template>
```

```coffee
<script lang="coffee">
import drag from '~/lib/drag.coffee'
nav = shallowRef()
export default {
setup: =>
  onMounted =>
    drag(nav.value)
    return
  {
    nav
  }
}
</script>
```

## main/boot.coffee

```coffee
_handle = (k, v)=>
  if v instanceof Function
    ipcMain.handle k.join('.'), (e,args)=>
      v.apply(e,args)

  for [name,func] from Object.entries v
    _handle [...k, name], func

  return

for [k,v] from Object.entries ipc
  _handle([k], v)

```

## web/src/lib/_on.coffee

```coffee
export default (elem, dict)=>
  for event,func of dict
    elem.addEventListener(event, func)
  =>
    for event,func of dict
      elem.removeEventListener(event, func)

```

## web/src/lib/on.coffee

```coffee
import $on from './_on.coffee'
export default (elem, dict)=>
  unbind = $on elem, dict
  onUnmounted unbind
  unbind
```