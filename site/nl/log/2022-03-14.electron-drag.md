# electron perfect drag and drop : een elegantere implementatie dan electron-drag

De productmanager heeft gevraagd of de werkbalk onder het elektronenvenster de rechtermuisknop kan blokkeren.

De native drag and drop in electron onder window ( `app-region:drag`) vreet de click event en er is geen manier om rechtsklik te blokkeren. Je moet het slepen en neerzetten handmatig uitvoeren.

Als je het mousemove event gebruikt om de eis te implementeren, gaat de muis, wanneer hij snel beweegt, buiten het bereik van het venster en volgt het venster de muis niet langer.

Ik heb deze eis geïmplementeerd op basis van de pointer event, met [setPointerCapture](https://developer.mozilla.org/zh-CN/docs/Web/API/Element/setPointerCapture) om de muispositie buiten het BrowserWindow vast te leggen, en het werkt perfect.

Deze oplossing is beter dan de electron-drag oplossing: het heeft platform-afhankelijke binaire afhankelijkheden en kan een pijn zijn om te upgraden en te packagen.

Ik ontdekte dat nog niemand deze oplossing op het web heeft genoemd, dus ik blog erover en deel het.

De codeverschillen tussen de implementaties worden hieronder alleen ter referentie opgesomd. De kerncode is `web/src/lib/drag.coffee` en `main/ipc/drag.coffee`.

## web/src/lib/drag.coffee

```coffee
import $on from '~/lib/on.coffee'
import ipc from '~/lib/ipc.coffee'
import platform from '@/config/platform.mjs'

{drag:{setBounds,getBounds}} = ipc
pointermove = 'pointermove'
IGNORE = new Set('SELECT BUTTON A INPUT TEXTAREA'.split ' ')
{round} = Math
export default main = (elem)=>
  if platform!='win32'
    return

  elem.style.appRegion = 'no-drag'

  moving = false

  init_w = init_h = init_x = init_y = init_top = init_left = undefined

  _move = (e)=>
    {screenX,screenY} = e
    setBounds(
      round screenX-init_x+init_left
      round screenY-init_y+init_top
      init_w
      init_h
    )
    return

  down = (e)=>
    if moving
      return
    if e.button!=0 # 鼠标左键
      return
    {target} = e
    p = e.target
    loop
      {nodeName} = p
      if IGNORE.has nodeName
        return
      if nodeName == 'BODY'
        break
      p = p.parentNode
    moving = true
    {screenX:init_x,screenY:init_y} = e

    elem.setPointerCapture e.pointerId
    elem.addEventListener pointermove,_move

    {
      x:init_left
      y:init_top
      height:init_h
      width:init_w
    } = await getBounds()

    console.log  await getBounds()
    return

  up = (e)=>
    if moving
      await _move(e)
      elem.releasePointerCapture e.pointerId
      elem.removeEventListener pointermove,_move
      moving = false
    return

  $on elem,{
    lostpointercapture:up
    pointercancel:up
    pointerdown:down
    pointerup:up
  }

  return
```

## main/ipc/drag.coffee

```coffee
export getBounds = ->
  @sender.getOwnerBrowserWindow().getBounds()

export setBounds = (x,y,width,height)->
  win = @sender.getOwnerBrowserWindow()
  # 不用 setPosition 是因为 https://github.com/electron/electron/issues/9477 browserWindow.setPosition(x,y) changed window size (windows/linux) with non default scaleLevel (125% for example)
  win.setBounds {
    x:Math.round(x)
    y:Math.round(y)
    width
    height
  }
  return
```

## main/ipc.coffee

```coffee
`export * as drag from './ipc/drag.coffee'`
```

## web/src/page/recbar.vue

```pug
<template lang="pug">
-nav(:class="{ pause }")
nav(:class="{ pause }" ref="nav")
</template>
```

```coffee
<script lang="coffee">
import drag from '~/lib/drag.coffee'
nav = shallowRef()
export default {
setup: =>
  onMounted =>
    drag(nav.value)
    return
  {
    nav
  }
}
</script>
```

## main/boot.coffee

```coffee
_handle = (k, v)=>
  if v instanceof Function
    ipcMain.handle k.join('.'), (e,args)=>
      v.apply(e,args)

  for [name,func] from Object.entries v
    _handle [...k, name], func

  return

for [k,v] from Object.entries ipc
  _handle([k], v)

```

## web/src/lib/_on.coffee

```coffee
export default (elem, dict)=>
  for event,func of dict
    elem.addEventListener(event, func)
  =>
    for event,func of dict
      elem.removeEventListener(event, func)

```

## web/src/lib/on.coffee

```coffee
import $on from './_on.coffee'
export default (elem, dict)=>
  unbind = $on elem, dict
  onUnmounted unbind
  unbind
```